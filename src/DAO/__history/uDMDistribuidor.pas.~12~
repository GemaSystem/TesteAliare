unit uDMDistribuidor;

interface

uses
  System.SysUtils, System.Classes, Data.FMTBcd, Datasnap.DBClient, Data.DB,
  Data.SqlExpr, Datasnap.Provider, VCL.Grids, uDistribuidorModel;

type
  TdtmDistribuidor = class(TDataModule)
    dspPesquisar: TDataSetProvider;
    sqlPesquisar: TSQLDataSet;
    cdsPesquisar: TClientDataSet;
  private
    { Private declarations }
  public
    { Public declarations }
    function GerarID:Integer;
    procedure Pesquisar(sNome: string; var aStringGrid : TStringGrid);
    procedure CarregarDistribuidor(oDistribuidor : TDistribuidor; iCodigo : Integer);
    function Inserir(oDistribuidor : TDistribuidor; out sErro : String):Boolean;
    function Alterar(oDistribuidor : TDistribuidor; out sErro : String):Boolean;
    function Excluir(iCodigo : Integer; out sErro : String):Boolean;
  end;

var
  dtmDistribuidor: TdtmDistribuidor;

implementation

{%CLASSGROUP 'Vcl.Controls.TControl'}

uses DMConexao;

{$R *.dfm}

{ TdtmDistribuidor }

function TdtmDistribuidor.Alterar(oDistribuidor: TDistribuidor;
  out sErro: String): Boolean;
var
  sqlAlterar : TSQLDataset;
begin
  sqlAlterar := TSQLDataset.Create(nil);
  try
    sqlAlterar.SQLConnection := dtmConexao.SqlConexao;
    sqlAlterar.CommandText := ' UPDATE DISTRIBUIDOR '+
                              ' SET NOME = :NOME, '+
                              ' CNPJ = :CNPJ '+
                              ' WHERE DISTRIBUIDOR_ID = :DISTRIBUIDOR_ID ';

    sqlAlterar.Params[0].AsString := oDistribuidor.Nome;
    sqlAlterar.Params[1].AsString := oDistribuidor.CNPJ;
    sqlAlterar.Params[2].AsInteger := oDistribuidor.id;

    try
      sqlAlterar.ExecSQL();
      Result := true;
    Except
      on e:Exception do
      begin
        sErro := 'Erro ao alterar produtor: '+e.Message;
        Result := false;
      end;
    end;
  finally
    FreeAndNil(sqlAlterar);
  end;

end;

procedure TdtmDistribuidor.CarregarDistribuidor(oDistribuidor: TDistribuidor;
  iCodigo: Integer);
var
  sqlDistribuidor : TSQLDataSet;
begin
  sqlDistribuidor := TSQLDataSet.Create(nil);
  try
    sqlDistribuidor.SQLConnection := dtmConexao.SqlConexao;
    sqlDistribuidor.CommandText := 'Select * from DISTRIBUIDOR WHERE DISTRIBUIDOR_ID = '+iCodigo.ToString;
    sqlDistribuidor.Open;

    oDistribuidor.Nome := sqlDistribuidor.FieldByName('Nome').AsString;
    oDistribuidor.CNPJ  := sqlDistribuidor.FieldByName('CNPJ').AsString;
    oDistribuidor.ID  := sqlDistribuidor.FieldByName('DISTRIBUIDOR_ID').AsInteger;
  finally
    FreeAndNil(sqlDistribuidor);
  end;
end;

function TdtmDistribuidor.Excluir(iCodigo: Integer; out sErro: String): Boolean;
var
  sqlDeletar : TSQLDataset;
begin
  sqlDeletar := TSQLDataset.Create(nil);
  try
    sqlDeletar.SQLConnection := dtmConexao.SqlConexao;
    sqlDeletar.CommandText := ' DELETE FROM DISTRIBUIDOR WHERE DISTRIBUIDOR_ID = :DISTRIBUIDOR_ID ';

    sqlDeletar.Params[0].AsInteger := iCodigo;

    try
      sqlDeletar.ExecSQL();
      Result := true;
    Except
      on e:Exception do
      begin
        sErro := 'Erro ao excluir produtor: '+e.Message;
        Result := false;
      end;
    end;
  finally
    FreeAndNil(sqlDeletar);
  end;
end;

function TdtmDistribuidor.GerarID: Integer;
var
  sqlSeq : TSQLDataset;
begin
  sqlSeq := TSQLDataset.Create(nil);
  try
    sqlSeq.SQLConnection := dtmConexao.SqlConexao;
    sqlSeq.CommandText := 'SELECT GEN_ID(GEN_DIST, 1) FROM RDB$DATABASE;';
    sqlSeq.Open;
    Result := sqlSeq.FieldByName('GEN_ID').AsInteger;
  finally
    FreeAndNil(sqlSeq);
  end;
end;

function TdtmDistribuidor.Inserir(oDistribuidor: TDistribuidor;
  out sErro: String): Boolean;
var
  sqlInsert : TSQLDataset;
begin
  sqlInsert := TSQLDataset.Create(nil);
  try
    sqlInsert.SQLConnection := dtmConexao.SqlConexao;
    sqlInsert.CommandText := ' Insert into DISTRIBUIDOR( '+
                             '                      DISTRIBUIDOR_ID, '+
                             '                      Nome, '+
                             '                      CNPJ)values( '+
                             '                      :DISTRIBUIDOR_ID, '+
                             '                      :NOME,  '+
                             '                      :CNPJ) ';

    sqlInsert.Params[0].AsInteger := GerarID;
    sqlInsert.Params[1].AsString := oDistribuidor.Nome;
    sqlInsert.Params[2].AsString := oDistribuidor.CNPJ;

    try
      sqlInsert.ExecSQL();
      Result := true;
    Except
      on e:Exception do
      begin
        sErro := 'Erro ao inserir produtor: '+e.Message;
        Result := false;
      end;
    end;
  finally
    FreeAndNil(sqlInsert);
  end;

end;

procedure TdtmDistribuidor.Pesquisar(sNome: string;
  var aStringGrid: TStringGrid);
var
 iTotal : Integer;
begin
  try
    iTotal := 1;

    cdsPesquisar.Active := False;

    if sNome.Trim.IsEmpty then
      cdsPesquisar.Params[0].AsString := '%'
    else
      cdsPesquisar.Params[0].AsString := '%'+sNome+'%';

    cdsPesquisar.Open;
    cdsPesquisar.DisableConstraints;
    cdsPesquisar.Last;

    //Configurar colunas StringGrid
    aStringGrid.RowCount := cdsPesquisar.RecordCount+1;
    aStringGrid.ColCount := 3;
    aStringGrid.Cells[0,0] := 'ID';
    aStringGrid.Cells[1,0] := 'Nome';
    aStringGrid.cells[2,0] := 'CNPJ';
    aStringGrid.ColWidths[0] := 50;
    aStringGrid.ColWidths[1] := 260;
    aStringGrid.ColWidths[2] := 90;

    //Popular linhas StringGrid
    cdsPesquisar.First;
    while not cdsPesquisar.Eof do
    begin
      aStringGrid.Cells[0,iTotal] := cdsPesquisar.FieldByName('DISTRIBUIDOR_ID').AsString;
      aStringGrid.Cells[1,iTotal] := cdsPesquisar.FieldByName('NOME').AsString;
      aStringGrid.Cells[2,iTotal] := cdsPesquisar.FieldByName('CNPJ').AsString;
      Inc(iTotal);
      cdsPesquisar.Next;
    end;
  finally
    cdsPesquisar.EnableConstraints;
    cdsPesquisar.Close;
  end;

end;

end.
